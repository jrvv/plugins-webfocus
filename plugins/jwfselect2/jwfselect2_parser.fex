-INCLUDE IBFS:/WFC/Repository/Public/system/plugins/jwfselect2/settings_jwfselect2.fex
-RUN
-SET &ECHO=ALL;
-SET &AUX_QUOTED = IF &JWF_COD_IS_NUM.QUOTEDSTRING EQ 'TRUE' THEN '' ELSE '.QUOTEDSTRING';
-SET &MAX_COMAND=IF &JWF_COD_IS_NUM.QUOTEDSTRING EQ 'TRUE' THEN 'MAX.' ELSE '';
TABLE FILE &JWF_MAST.EVAL
SUM
COMPUTE TOT/P21=CNT.DST.&JWF_COD.EVAL;
-*-INCLUDE IBFS:/WFC/Repository/DevTests/pd_autocomplete_multiple/Internal/jwfselect2_dependencies.fex
-REPEAT DEPEN_LOOP1 FOR &CONT FROM 1 TO 500 STEP 1;
-IF &JWF_DEPENDENCY&CONT.EVAL.EXISTS EQ 0 THEN GOTO NOMOREDEPEN1;
-DEFAULT &DEPENDENCY&CONT.EVAL = '_FOC_NULL';
-SET &DEPENDENCY&CONT.EVAL = TRUNCATE(&DEPENDENCY&CONT.EVAL.QUOTEDSTRING);
-IF &DEPENDENCY&CONT.EVAL.QUOTEDSTRING EQ '' THEN GOTO DEPEN_LOOP1;
WHERE &JWF_DEPENDENCY&CONT.EVAL IN (&DEPENDENCY&CONT.EVAL)
-DEPEN_LOOP1
-NOMOREDEPEN1
-*
-IF &SHOW_SELECTED_OPTIONS EQ 'TRUE' THEN GOTO SEL_LOOP1;
-REPEAT SEL_LOOP1 FOR &CONT FROM 1 TO 500 STEP 1;
-IF &OPT_SEL&CONT.EVAL.EXISTS EQ 0 THEN GOTO NOMORESEL1;
-DEFAULT &OPT_SEL&CONT.EVAL = '_FOC_NULL';
-SET &OPT_SEL&CONT.EVAL = TRUNCATE(&OPT_SEL&CONT.EVAL.QUOTEDSTRING);
-IF &OPT_SEL&CONT.EVAL.QUOTEDSTRING EQ '' THEN GOTO SEL_LOOP1;
WHERE &JWF_COD.EVAL NE &OPT_SEL&CONT.EVAL&AUX_QUOTED.EVAL
-SEL_LOOP1
-NOMORESEL1
-*
-IF &INPUT.QUOTEDSTRING EQ '' THEN GOTO NOSEARCH1;
-*WHERE UPPER(&JWF_DESC.EVAL) CONTAINS UPPER(&INPUT.QUOTEDSTRING)
WHERE UPPER(&JWF_DESC.EVAL) CONTAINS UPPER(&INPUT.QUOTEDSTRING)
-NOSEARCH1

ON TABLE SET HOLDLIST PRINTONLY
ON TABLE SAVE AS XXLINES
END
-RUN
-*
-IF &LINES EQ 0 THEN GOTO NOT_LINES;
-*
-READ XXLINES &TOTLINES.A21
-RUN
-*
-SET &TOTLINES=TRUNCATE(&TOTLINES);
-NOT_LINES
-*
-IF &TOTLINES EQ 0 THEN GOTO NOT_AUTO;

TABLE FILE &JWF_MAST.EVAL
SUM
    &MAX_COMAND.EVAL&JWF_COD.EVAL AS 'COD'
	COMPUTE XWEB/A1020=&JWF_DESC.EVAL; NOPRINT
	COMPUTE XDESC/A1020=ESCAPEJSON(XWEB); AS 'DESC'
	COMPUTE XTOOLTIP/A1050='Tooltip: '|XDESC; AS 'TOOLTIP'
	COMPUTE TOT_LINES/P21=&TOTLINES.EVAL;
-IF &ORDER_BY EQ 'DESC' THEN GOTO ORDER_TEXT;
BY &ORDER.EVAL &LINES_PAGE.EVAL &JWF_COD.EVAL NOPRINT
BY &ORDER.EVAL &LINES_PAGE.EVAL &JWF_DESC.EVAL NOPRINT
-GOTO END_ORDER;
-ORDER_TEXT
BY &ORDER.EVAL &LINES_PAGE.EVAL &JWF_DESC.EVAL NOPRINT
BY &ORDER.EVAL &LINES_PAGE.EVAL &JWF_COD.EVAL NOPRINT
-END_ORDER
-*-INCLUDE IBFS:/WFC/Repository/DevTests/pd_autocomplete_multiple/Internal/jwfselect2_dependencies.fex
-REPEAT DEPEN_LOOP2 FOR &CONT FROM 1 TO 500 STEP 1;
-IF &JWF_DEPENDENCY&CONT.EVAL.EXISTS EQ 0 THEN GOTO NOMOREDEPEN2;
-DEFAULT &DEPENDENCY&CONT.EVAL = '_FOC_NULL';
-SET &DEPENDENCY&CONT.EVAL = TRUNCATE(&DEPENDENCY&CONT.EVAL.QUOTEDSTRING);
-IF &DEPENDENCY&CONT.EVAL.QUOTEDSTRING EQ '' THEN GOTO DEPEN_LOOP2;
WHERE &JWF_DEPENDENCY&CONT.EVAL IN (&DEPENDENCY&CONT.EVAL)
-DEPEN_LOOP2
-NOMOREDEPEN2
-IF &SHOW_SELECTED_OPTIONS EQ 'TRUE' THEN GOTO SEL_LOOP2;
-REPEAT SEL_LOOP2 FOR &CONT FROM 1 TO 500 STEP 1;
-IF &OPT_SEL&CONT.EVAL.EXISTS EQ 0 THEN GOTO NOMORESEL2;
-DEFAULT &OPT_SEL&CONT.EVAL = '_FOC_NULL';
-SET &OPT_SEL&CONT.EVAL = TRUNCATE(&OPT_SEL&CONT.EVAL.QUOTEDSTRING);
-IF &OPT_SEL&CONT.EVAL.QUOTEDSTRING EQ '' THEN GOTO SEL_LOOP2;
WHERE (&JWF_COD.EVAL) NE &OPT_SEL&CONT.EVAL&AUX_QUOTED.EVAL
-SEL_LOOP2
-NOMORESEL2

-IF &INPUT.QUOTEDSTRING EQ '' THEN GOTO NOSEARCH2;
-*WHERE UPPER(&JWF_DESC.EVAL) CONTAINS UPPER(&INPUT.QUOTEDSTRING)
 WHERE UPPER(&JWF_DESC.EVAL) CONTAINS &INPUT.QUOTEDSTRING
-*WHERE D_PERSON_CUSTOMER.DESC_PERSON  CONTAINS &INPUT.QUOTEDSTRING OR  D_PERSON_CUSTOMER.CIF CONTAINS &INPUT.QUOTEDSTRING OR DB_EXPR(CONVERT(VARCHAR,"ID_PERSON")) CONTAINS &INPUT.QUOTEDSTRING
-NOSEARCH2

IF READLIMIT EQ &LINES_PAGE.EVAL

-IF &LAST_LOAD.QUOTEDSTRING EQ '' THEN GOTO NOT_LAST;
-* Filter with the last row on the previous execution
-IF (&ORDER_BY.QUOTEDSTRING EQ ' ') THEN GOTO FILTRO_ID;
-*WHERE (&JWF_DESC.EVAL GT &LAST_LOAD.QUOTEDSTRING ) OR ( (&JWF_DESC.EVAL EQ &LAST_LOAD.QUOTEDSTRING) AND (&JWF_COD.EVAL GT &LAST_LOAD_ID&AUX_QUOTED.EVAL) )
WHERE (&JWF_DESC.EVAL GT &LAST_LOAD.QUOTEDSTRING );
-GOTO NOT_LAST;
-FILTRO_ID
WHERE  (&JWF_COD.EVAL GT &LAST_LOAD_ID&AUX_QUOTED.EVAL);
-NOT_LAST

ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
-IF &DEBUG_MODE EQ 'TRUE' THEN GOTO NO_JSON;
ON TABLE PCHOLD FORMAT JSON
-NO_JSON
ON TABLE NOTOTAL
ON TABLE SET CACHELINES 100
END
-RUN

-NOT_AUTO
-IF &TOTLINES NE 0 THEN GOTO YES_AUTO;
TABLEF FILE SYSCOLUM
PRINT TBNAME NOPRINT
COMPUTE EMPTY_JSON/A14='{"records":[]}'; AS ''
IF RECORDLIMIT EQ 1
ON TABLE SET LINES 998
ON TABLE SET PAGE NOPAGE
ON TABLE SET HOLDLIST PRINTONLY
-IF &DEBUG_MODE EQ 'TRUE' THEN GOTO NO_JSON2;
ON TABLE PCHOLD FORMAT WP
-NO_JSON2
END
-RUN
-*
-YES_AUTO